FUNCTION_BLOCK nutria	// Block definition (there may be more than one block per file)

// Define variables de entrada
VAR_INPUT
    carbohidratos : REAL;
    proteinas : REAL;
    grasas : REAL;
    vitaminas : REAL;
    minerales : REAL;
    agua : REAL;
END_VAR

// Define variables de salida
VAR_OUTPUT
    nutricion : REAL;
END_VAR

// Fuzzify variable de entrada carbohidratos
FUZZIFY carbohidratos
    TERM pocos := (0,1) (70,0.5) (130, 0); 
    TERM normal := (100,0) (155,1) (210,1) (300,0);
    TERM muchos := (240,0) (260,1) (400,1);
END_FUZZIFY

// Fuzzify variable de entrada proteinas
FUZZIFY proteinas
    TERM pocas := (0,1) (35,1) (70,0);
    TERM normal := (50,0) (95,1) (130,1) (175,0);
    TERM muchas := (145,0) (160,1) (300,1);
END_FUZZIFY

// Fuzzify variable de entrada grasas
FUZZIFY grasas
    TERM pocas := (0,1) (1,1) (1.6,0);
    TERM normal := (1.5,0) (2,1) (2.6,0);
    TERM muchas := (2.2,0) (2.6,1) (5,1);
END_FUZZIFY

// Fuzzify variable de entrada vitaminas
FUZZIFY vitaminas
    TERM pocas := (0,1) (3,1) (5,0);
    TERM normal := (4,0) (6,1) (8,1) (11,0);
    TERM muchas := (9,0) (11,1) (13,1);
END_FUZZIFY

// Fuzzify variable de entrada minerales
FUZZIFY minerales
    TERM pocos := (0,1) (2,1) (4,0);
    TERM suficientes := (3,0) (5,1) (7,1);
END_FUZZIFY

// Fuzzify variable de entrada agua
FUZZIFY agua
    TERM poca := (0,1) (1.5,1) (2.5,0);
    TERM regular := (2.2,0) (2.7,1) (3.3,1) (4.8,0);
    TERM mucha := (3.5,0) (5.4,1) (9,1);
END_FUZZIFY

// Defzzzify variable de salida nutrición
DEFUZZIFY nutricion
    TERM muy_mal := (0,1) (15,1) (20,0);
    TERM mal := (14,0) (25,1) (40,0);
    TERM regular := (35,0) (50,1) (70,0);
    TERM buena := (65,0) (75,1) (80,1) (90,0);
    TERM excelente := (85,0) (91,1) (100,1);

    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY


RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    ACT : MIN;  // Use 'min' activation method
    ACCU : MAX; // Use 'max' accumulation method

    RULE 1 : IF  carbohidratos IS pocos AND proteinas IS pocas AND grasas IS pocas AND vitaminas IS pocas AND minerales IS pocos AND agua IS poca
                THEN nutricion IS muy_mal;

    RULE 2 : IF  carbohidratos IS normal AND proteinas IS normal AND grasas IS normal AND vitaminas IS normal AND minerales IS suficientes AND agua IS regular  
                THEN nutricion IS buena;

    RULE 3 : IF  carbohidratos IS muchos AND proteinas IS muchas AND grasas IS muchas AND vitaminas IS muchas AND minerales IS suficientes AND agua IS mucha  
                THEN nutricion IS mal;

    RULE 4 : IF  carbohidratos IS normal AND proteinas IS pocas AND grasas IS muchas AND vitaminas IS pocas AND minerales IS pocos
                THEN nutricion IS muy_mal;
    
    RULE 5 : IF  carbohidratos IS muchos AND proteinas IS muchas AND grasas IS normal AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular 
                THEN nutricion is excelente;

    RULE 6 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS muchas AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular  
                THEN nutricion is buena;
    
    RULE 7 : IF  carbohidratos IS muchos AND proteinas IS normal AND grasas IS normal AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular   
                THEN nutricion is buena;

    RULE 8 : IF  carbohidratos IS pocos AND proteinas IS normal AND grasas IS normal AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular 
                THEN nutricion is regular;
    
    RULE 9 : IF  carbohidratos IS normal AND proteinas IS normal AND grasas IS normal AND vitaminas IS pocas AND minerales IS pocos AND agua IS regular  
                THEN nutricion is regular;

    RULE 10 : IF  carbohidratos IS pocos AND grasas IS muchas AND vitaminas IS pocas AND minerales IS pocos AND agua IS poca 
                THEN nutricion is muy_mal;
    
    RULE 11 : IF  carbohidratos IS pocos AND proteinas IS muchas AND grasas IS muchas AND vitaminas IS pocas AND minerales IS pocos AND agua IS regular 
                THEN nutricion is mal;

    RULE 12 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS normal AND vitaminas IS normal AND minerales IS suficientes AND agua IS mucha 
                THEN nutricion is buena;

    RULE 13 : IF  vitaminas IS pocas AND minerales IS pocos AND agua IS regular 
                THEN nutricion is mal;

    RULE 14 : IF  carbohidratos IS normal AND proteinas IS normal AND grasas IS pocas AND agua IS regular 
                THEN nutricion is buena;

    RULE 15 : IF  carbohidratos IS normal AND proteinas IS normal AND grasas IS muchas AND agua IS regular
                THEN nutricion is buena;

    RULE 16 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS pocas AND vitaminas IS muchas AND agua IS poca 
                THEN nutricion is mal;

    RULE 17 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS muchas AND vitaminas IS muchas AND agua IS poca
                THEN nutricion is mal;

    RULE 18 : IF  carbohidratos IS pocos AND proteinas IS pocas
                THEN nutricion is mal;

    RULE 19 : IF  carbohidratos IS pocos AND proteinas IS pocas AND grasas IS pocas 
                THEN nutricion is muy_mal;

    RULE 20 : IF  carbohidratos IS pocos AND proteinas IS pocas AND grasas IS normal
                THEN nutricion is muy_mal;

    RULE 21 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS pocas 
                THEN nutricion is regular;

    RULE 22 : IF  carbohidratos IS normal AND proteinas IS muchas AND grasas IS muchas
                THEN nutricion is regular;

    RULE 23 : IF  vitaminas IS normal AND minerales IS suficientes AND agua IS regular 
                THEN nutricion is regular;

    RULE 24 : IF  grasas IS pocas AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular 
                THEN nutricion is buena;

    RULE 25 : IF  grasas IS muchas AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular
                THEN nutricion is buena;

    RULE 26 : IF  carbohidratos IS normal AND proteinas IS normal AND grasas IS normal AND vitaminas IS muchas AND minerales IS suficientes AND agua IS regular 
                THEN nutricion is excelente;
    
END_RULEBLOCK

END_FUNCTION_BLOCK